(*
 * This library contains the definitions of the MLRISC IR 
 * and simple optimizations.
 *)

Library 
	signature MLTREE_SIMPLIFIER
	functor MLTreeSimplifier
	signature CLUSTER_GRAPH
	functor ClusterGraphFn
	signature CLUSTER_VIEWER
	functor ClusterViewer
	signature DATAFLOW_ANALYZER
	signature DATAFLOW_PROBLEM
	functor DataflowFn
	signature CONTROL_FLOW_GRAPH_GEN
	functor ControlFlowGraphGenFn
	functor CFGGen
	signature CFG_UTIL
	functor CFGUtilFn
	signature CONTROL_FLOW_GRAPH
	functor ControlFlowGraphFn
	signature CLUSTER2CFG
	functor Cluster2CFGFn
	signature CFG2CLUSTER
	functor CFG2ClusterFn
	signature FORMAT_INSTRUCTION
	functor FormatInstructionFn
	signature MLRISC_IDEFS
	functor IDefsFn
	signature MLRISC_IR
	functor MLRISC_IRFn
	signature LIVENESS_ANALYSIS
	functor LivenessAnalysisFn
	signature INSERT_PREHEADERS
	functor InsertPreheadersFn
	signature RESHAPE_BRANCHES
	functor ReshapeBranchesFn
	signature STATIC_BRANCH_PREDICTION
	functor StaticBranchPredictionFn
	signature MLRISC_GLUE
	functor MLRISCGlue
is

	basis.cm
	comp-lib.cm
	smlnj-lib.cm
	
	MLRISC.cm
	MLRISC-Lib.cm
	MLRISC-Graphs.cm
	MLRISC-Visual.cm
	MLRISC-ir.cm

	(* These modules perform very simple local optimizations on MLTREE
	 *)
	mltree/mltree-simplify.sig
	mltree/mltree-simplify.sml
	
	(* These provide a graph view for clusters.  Required if you 
	 * want to perform any non-trivial analysis on the program.
	 * Also required if you want to use the visualization stuff on clusters.
	 *)
	cluster/clusterGraph.sig
	cluster/clusterGraph.sml
	cluster/clusterViewer.sig
	cluster/clusterViewer.sml
	
	(* MLRISC IR for optimizations. These are necessary only if you want
	 * to use various optional MLRISC optimizations.
	 *)
	IR/dataflow.sig
	IR/dataflow.sml
	IR/mlrisc-cfg-gen.sig
	IR/mlrisc-cfg-gen.sml
	IR/mlrisc-cfg-gen2.sml
	IR/mlrisc-cfg-util.sig
	IR/mlrisc-cfg-util.sml
	IR/mlrisc-cfg.sig
	IR/mlrisc-cfg.sml
	IR/mlrisc-cluster2cfg.sml
	IR/mlrisc-cfg2cluster.sml
	IR/mlrisc-format-insn.sml
	IR/mlrisc-idefs.sig
	IR/mlrisc-idefs.sml
	IR/mlrisc-ir.sig
	IR/mlrisc-ir.sml
	IR/mlrisc-liveness.sml
	IR/mlrisc-preheaders.sig
	IR/mlrisc-preheaders.sml
	IR/mlrisc-reshape-branches.sig
	IR/mlrisc-reshape-branches.sml
	IR/static-branch-prediction.sig
	IR/static-branch-prediction.sml
	
	(* Glue modules.  These are required only if you want to 
	 * play with optimizations.
	 *)
	Glue/mlrisc-glue.sig
	Glue/mlrisc-glue.sml
