(* Copyright YALE FLINT PROJECT 1997 *)
(* smlnj/viscomp/core.cm *)

(*
 * This used to be called all-files.cm, then sources.cm, and even later
 * viscomp-lib.cm (at which point it became "just a library").
 * It is now reduced to the machine-independent core part of the
 * library.  Machine-dependent parts are in viscomp/<arch>.cm.
 *   M.Blume (11/1999)
 *
 * Moved to smlnj/viscomp for better path-anchor organization.
 *   M.Blume (03/2000)
 *)

Library
	signature GENERIC_VC
	signature MACHDEP_VC

	structure GenericVC

	(* eventually we should export more of these guys... *)
	structure DynamicEnv
	structure PrettyPrint

	structure PickleSymPid
	structure UnpickleSymPid
	structure PickMod
	structure UnpickMod
	structure GenModIdMap
	structure CompBasic
	structure CoreSym

	structure FLINT

        (* To make it possible to dynamically link in the optimizer, 
         * we have to export the following to the library.
         * A.Leung.
         *)
        signature MACHINE_GEN
        signature INVOKE_GC
        signature CPSREGS

        structure CPSRegions
        structure SMLGCType
        structure SMLGCMap

	(* To be able to separate machine-dependent parts of viscomp-lib.cm
	 * from machine-independent parts, we must also export the
	 * the following things.
	 *  M.Blume (11/1999) *)

	signature MACH_SPEC

	structure ErrorMsg
	structure Control
	structure DefaultMachSpec
	structure SMLNJConstant
	structure SMLNJLabelExp
	structure SMLNJMLTreeExt
        functor   SMLNJMLTreeExtComp
	structure CodeString

	functor PseudoOpsBig
	functor PseudoOpsLittle
	functor MachineGen
	functor RegAlloc
	functor FLINTComp
	functor MachDepVCFun
is
	(* reference to our only group... *)
	compiler/viscomp-core.cm
